buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}


repositories {
    ivy {
        name 'Forge FS legacy'
        artifactPattern "http://files.minecraftforge.net/[module]/[module]-dev-[revision].[ext]"
    }
    ivy {
        name 'CB FS'
        artifactPattern "http://www.chickenbones.craftsaddle.org/Files/New_Versions/1.6.4/[module]-dev%20[revision].[ext]"
    }
    maven {
        name 'ForgeFS'
        url 'http://files.minecraftforge.net/maven'
     }
}

apply plugin: 'forge'

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.mlcore.version}"
group = "ml.core"
archivesBaseName = "MLCore"

if (System.getenv("BUILD_NUMBER") != null)
    version += ".${System.getenv("BUILD_NUMBER")}"

ext.simpleVersion = version
version = "MC${config.minecraft.version}-${project.version}"

dependencies {
    compile name: 'CodeChickenLib', version: "${config.minecraft.version}-${config.CCLIB.version}", ext: 'jar'
    compile name: 'NotEnoughItems', version: "${config.NEI.version}", type: 'jar'
}

apply from: 'gradle/forge.gradle'
apply from: 'gradle/artifact.gradle'
apply from: 'gradle/release.gradle'
